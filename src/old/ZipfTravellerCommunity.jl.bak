"""
A 'Zipfian traveller community': speakers are located on a two-dimensional
lattice. Each speaker has a 'home' cell on the lattice, but also has the
option of travelling to another cell. The probability to travel to a cell
with coordinates ``x'`` and ``y'`` scales as the inverse of the distance
between that cell and the speaker's home (coordinates ``x`` and ``y``),
measured as the Manhattan distance ``d``:

```math
P \\propto \\frac{1}{(1 + d)^\\lambda}
```

where ``\\lambda`` is a scaling parameter.
"""
mutable struct ZipfTravellerCommunity <: AbstractDiscretePlanarCommunity
  # Census
  census::Array{AbstractSpeaker}

  # Speakers' homes
  homes::Array{Point{Int}}

  # Speakers' temporary (within-epoch) locations
  locations::Array{Point{Int}}

  # Lattice size
  size::Int

  # Decay exponent
  de::Float64

  # Probabilities
  probabilities::Array{Float64}

  # Distance function
  distance::Function

  # Range of possible distances
  possible_distances::Dict{Point{Int}, Array{Int}}

  # How many actions have occurred
  actions::Int

  # Cells at a given distance from given cell
  lookup::Dict{Tuple{Point{Int}, Int}, Array{Point{Int}}}
end


"""
    ZipfTravellerCommunity(K::Int, lambda::Float64)

Construct a Zipf traveller community of size `K` by `K` with scaling
parameter `lambda`.
"""
function ZipfTravellerCommunity(K::Int, lambda::Float64)
  # Possible distances on lattice
  possdist = collect(0:2*(K-1))

  # Possible distances for each cell
  possdistcell = Dict{Point{Int}, Array{Int}}()

  # Make lookup table
  lup = Dict{Tuple{Point{Int}, Int}, Array{Point{Int}}}()
  for x in 1:K
    for y in 1:K
      point = Point{Int}(x, y)
      for dist in possdist
        for i in 1:K
          for j in 1:K
            point2 = Point{Int}(i, j)
            if manhattan(point, point2) == dist
              if !haskey(possdistcell, point)
                possdistcell[point] = Array{Int}(undef, 0)
              end
              push!(possdistcell[point], dist)
              if !haskey(lup, (point, dist))
                lup[(point, dist)] = Array{Point{Int}}(undef, 0)
              end
              push!(lup[(point, dist)], point2)
            end
          end
        end
      end
      possdistcell[point] = unique(possdistcell[point])
    end
  end

  # Construct
  ZipfTravellerCommunity(Array{AbstractSpeaker}(undef, 0), Array{Point{Int}}(undef, 0), Array{Point{Int}}(undef, 0), K, lambda, zipf(2*(K-1) + 1, lambda), manhattan, possdistcell, 0, lup)
end


"""
    inject!(x::AbstractSpeaker, y::ZipfTravellerCommunity)

Insert a speaker into a Zipf traveller community.
"""
function inject!(x::AbstractSpeaker, y::ZipfTravellerCommunity)
  push!(y.census, x)
  home = Point{Int}(rand(1:y.size), rand(1:y.size))
  push!(y.homes, home)
  push!(y.locations, home)
end


"""
    eject!(x::Int, y::ZipfTravellerCommunity)

Remove a speaker from a Zipf traveller community.
"""
function eject!(x::Int, y::ZipfTravellerCommunity)
  splice!(y.census, x)
  splice!(y.homes, x)
  splice!(y.locations, x)
end


"""
    travel!(x::Int, y::ZipfTravellerCommunity)

Make speaker with index `x` travel in a Zipf traveller community.
"""
function travel!(x::Int, y::ZipfTravellerCommunity)
  home = y.homes[x]
  loca = y.locations[x]
  possdist = y.possible_distances[home]
  dist = Distributions.wsample(possdist, zipf(maximum(possdist) + 1, y.de))
  y.locations[x] = rand(y.lookup[(home, dist)])
end


"""
    travel!(x::ZipfTravellerCommunity)

Make every speaker in a Zipf traveller community travel.
"""
function travel!(x::ZipfTravellerCommunity)
  for s in 1:length(x.census)
    travel!(s, x)
  end
end


"""
    rendezvous!(x::ZipfTravellerCommunity, y::Int, z::Int)

Rendezvous between speakers indexed by `y` and `z` in a Zipf traveller
community. Successful if and only if the speakers are currently occupying
the same lattice cell, i.e. if their Manhattan distance on the lattice
is zero.
"""
function rendezvous!(x::ZipfTravellerCommunity, y::Int, z::Int)
  manhattan(x.locations[y], x.locations[z]) == 0 ||Â return false
  act!(x.census[y], x.census[z])
  act!(x.census[z], x.census[y])
  x.actions += 2
end

